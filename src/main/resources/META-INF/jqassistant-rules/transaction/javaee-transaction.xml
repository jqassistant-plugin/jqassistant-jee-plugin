<jqassistant-rules xmlns="http://schema.jqassistant.org/rule/v2.2"
                   xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
                   xsi:schemaLocation="http://schema.jqassistant.org/rule/v2.2 https://jqassistant.github.io/jqassistant/current/schema/jqassistant-rule-v2.2.xsd">


    <concept id="javaee-transaction:TransactionalMethodByAnnotatedMethod">
        <providesConcept refId="jee-transaction:TransactionalMethod" />
        <description>
            Labels all methods which are annotated with "javax.transaction.Transactional", with "JEE" and "Transactional".
        </description>
        <cypher><![CDATA[
           MATCH
             (type:Type)-[:DECLARES]->(transactionalMethod:Method),
             (transactionalMethod:Method)-[:ANNOTATED_BY]->()-[:OF_TYPE]->(annotationType:Type)
           WHERE
             annotationType.fqn="javax.transaction.Transactional"
           AND NOT
             transactionalMethod.visibility = "private"
           SET
             transactionalMethod:JEE:Transactional
           RETURN
             type as Type, transactionalMethod as TransactionalMethod
        ]]></cypher>
    </concept>

    <concept id="javaee-transaction:TransactionalMethodByAnnotatedClass">
        <providesConcept refId="jee-transaction:TransactionalMethod" />
        <description>
            Labels methods of classes which are directly or indirectly annotated with "javax.transaction.Transactional" with "JEE" and "Transactional".
        </description>
        <cypher><![CDATA[
           MATCH
             (transactionalClass:Type)-[:EXTENDS|IMPLEMENTS*0..]->(:Type)-[:ANNOTATED_BY]->()-[:OF_TYPE]->(annotationType:Type),
             (transactionalClass)-[:DECLARES]->(transactionalMethod:Method)
           WHERE
             annotationType.fqn="javax.transaction.Transactional"
             AND NOT (
               transactionalMethod:Constructor
               OR (transactionalMethod.static IS NOT NULL and transactionalMethod.static)
               OR transactionalMethod.visibility = "private"
             )
           SET
             transactionalMethod:JEE:Transactional
           RETURN
             transactionalClass as TransactionalClass, collect(transactionalMethod) as TransactionalMethods
        ]]></cypher>
    </concept>

    <concept id="javaee-transaction:TypeLevelTransactionPropagation">
        <providesConcept refId="jee-transaction:TransactionPropagation"/>
        <requiresConcept refId="jee-transaction:TransactionalMethod"/>
        <requiresConcept refId="ejb:EJB"/>
        <description>
            Identification of type level transaction propagation semantics. The configured transaction propagation at type level applies to all declared transactional methods.
        </description>
        <cypher><![CDATA[
            WITH
                [
                    { annotationFqn: 'javax.ejb.TransactionAttribute', propagationAttributeName: 'value' },
                    { annotationFqn: 'javax.transaction.Transactional', propagationAttributeName: 'value' }
                ] as supportedAnnotationTypes
            UNWIND
                supportedAnnotationTypes AS supportedAnnotationType
            MATCH
                (artifact:Artifact)-[:CONTAINS]->(transactionalType:Type),
                (transactionalType)-[:EXTENDS|IMPLEMENTS*0..]->(:Type)-[:ANNOTATED_BY]->(annotation:Java:Annotation),
                (transactionalType)-[:DECLARES]->(transactionalMethod:Method:Transactional)
            WHERE
                (annotation)-[:OF_TYPE]->(:Type {fqn: supportedAnnotationType.annotationFqn}) OR
                transactionalType:JEE:EJB
            WITH
                transactionalType, transactionalMethod, annotation, supportedAnnotationType
            OPTIONAL MATCH
                (annotation)-[:HAS]->(propagation:Value {name: supportedAnnotationType.propagationAttributeName})-[:IS]->(propagationType:Java:Field)
            WITH
                COALESCE(SPLIT(propagationType.signature,' ')[1], 'REQUIRED') AS effectivePropagationType,
                transactionalType,
                transactionalMethod
            SET
                transactionalMethod.transactionPropagation = effectivePropagationType
            RETURN
                transactionalType as TransactionalType,
                transactionalMethod as TransactionalMethod,
                transactionalMethod.transactionPropagation AS TransactionPropagation
            ORDER BY
                transactionalType.fqn,
                transactionalMethod.signature
        ]]></cypher>
    </concept>

    <concept id="javaee-transaction:MethodLevelTransactionPropagation">
        <providesConcept refId="jee-transaction:TransactionPropagation"/>
        <requiresConcept refId="jee-transaction:TransactionalMethod"/>
        <!-- Ensure that method-level annotations override type-level annotations -->
        <requiresConcept refId="javaee-transaction:TypeLevelTransactionPropagation"/>
        <description>
            Identification of method level transaction propagation semantics. The configured transaction propagation at method level overrides the type level configuration.
        </description>
        <cypher><![CDATA[
            WITH
                [
                    { annotationFqn: 'javax.ejb.TransactionAttribute', propagationAttributeName: 'value' },
                    { annotationFqn: 'javax.transaction.Transactional', propagationAttributeName: 'value' }
                ] as supportedAnnotationTypes
            UNWIND
                supportedAnnotationTypes AS supportedAnnotationType
            MATCH
                (artifact:Artifact)-[:CONTAINS]->(transactionalType:Java:Type),
                (transactionalType)-[:EXTENDS|IMPLEMENTS*0..]->(:Type)-[:DECLARES]->(transactionalMethod:Method:Transactional)-[:ANNOTATED_BY]->(annotation:Java:Annotation),
                (annotation)-[:OF_TYPE]->(annotationType:Type {fqn: supportedAnnotationType.annotationFqn})
            OPTIONAL MATCH
                (annotation)-[:HAS]->(propagation:Value {name: supportedAnnotationType.propagationAttributeName})-[:IS]->(propagationType:Java:Field)
            WITH
                COALESCE(SPLIT(propagationType.signature,' ')[1], 'REQUIRED') AS effectivePropagationType,
                transactionalType,
                transactionalMethod
            SET
                transactionalMethod.transactionPropagation = effectivePropagationType
            RETURN
                transactionalType as TransactionalType,
                transactionalMethod as TransactionalMethod,
                transactionalMethod.transactionPropagation AS TransactionPropagation
            ORDER BY
                transactionalType.fqn,
                transactionalMethod.signature
        ]]></cypher>
    </concept>
    
</jqassistant-rules>