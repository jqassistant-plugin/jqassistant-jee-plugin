<jqassistant-rules xmlns="http://schema.jqassistant.org/rule/v1.10"
                   xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
                   xsi:schemaLocation="http://schema.jqassistant.org/rule/v1.10 http://schema.jqassistant.org/rule/jqassistant-rule-v1.10.xsd">

    <concept id="jakarta-jpa2:Entity">
        <providesConcept refId="jpa2:Entity"/>
        <description>Labels all types annotated with @jakarta.persistence.Entity with "Jpa" and "Entity".</description>
        <cypher><![CDATA[
            MATCH
              (t:Type)-[:ANNOTATED_BY]->()-[:OF_TYPE]->(a:Type)
            WHERE
              a.fqn="jakarta.persistence.Entity"
            SET
              t:Jpa:Entity
            RETURN
              t AS JpaEntity
        ]]></cypher>
    </concept>

    <concept id="jakarta-jpa2:Embeddable">
        <providesConcept refId="jpa2:Embeddable"/>
        <description>Labels all types annotated with @jakarta.persistence.Embeddable with "Jpa" and "Embeddable".</description>
        <cypher><![CDATA[
            MATCH
              (t:Type)-[:ANNOTATED_BY]->()-[:OF_TYPE]->(a:Type)
            WHERE
              a.fqn="jakarta.persistence.Embeddable"
            SET
              t:Jpa:Embeddable
            RETURN
              t AS JpaEmbeddable
        ]]></cypher>
    </concept>

    <concept id="jakarta-jpa2:Embedded">
        <providesConcept refId="jpa2:Embedded"/>
        <description>Labels all fields or methods annotated with @jakarta.persistence.Embedded with "Jpa" and "Embedded".</description>
        <cypher><![CDATA[
            MATCH
              (t:Type)-[:DECLARES]->(member),
              (member)-[:ANNOTATED_BY]->()-[:OF_TYPE]->(a:Type)
            WHERE
              (member:Field or member:Method)
              and a.fqn="jakarta.persistence.Embedded"
            SET
              member:Jpa:Embedded
            RETURN
              member AS JpaEmbedded
        ]]></cypher>
    </concept>

    <concept id="jakarta-jpa2:EmbeddedId">
        <providesConcept refId="jpa2:EmbeddedId"/>
        <description>Labels all fields or methods annotated with @jakarta.persistence.EmbeddedId with "Jpa" and "Embedded".</description>
        <cypher><![CDATA[
            MATCH
              (t:Type)-[:DECLARES]->(member),
              (member)-[:ANNOTATED_BY]->()-[:OF_TYPE]->(a:Type)
            WHERE
              (member:Field or member:Method)
              and a.fqn="jakarta.persistence.EmbeddedId"
            SET
              member:Jpa:EmbeddedId
            RETURN
              member AS EmbeddedId
        ]]></cypher>
    </concept>

    <concept id="jakarta-jpa2:NamedQuery">
        <providesConcept refId="jpa2:NamedQuery"/>
        <description>Creates a node labeled with "Jpa" and "NamedQuery" for each @NamedQuery defined for an entity.
            Furthermore a relation DEFINES is created between the entity and the named query.
        </description>
        <cypher><![CDATA[
            MATCH
              (entity:Jpa:Entity),
              (entity)-[:ANNOTATED_BY]->(namedQueries:Annotation)-[:OF_TYPE]-(:Type{fqn:'jakarta.persistence.NamedQueries'}),
              (namedQueries)-[:HAS]->(:Value:Array)-[:CONTAINS]->(namedQuery),
              (namedQuery)-[:OF_TYPE]->(:Type{fqn:'jakarta.persistence.NamedQuery'}),
              (namedQuery)-[:HAS]->(nameAttribute:Value{name:'name'}),
              (namedQuery)-[:HAS]->(queryAttribute:Value{name:'query'})
            MERGE
              (entity)-[:DEFINES]->(n:Jpa:NamedQuery {name:nameAttribute.value})
            SET
              n.query=queryAttribute.value
            RETURN
              entity as Entity, n.name as Name, n.query as Query
            UNION ALL
            MATCH
              (entity)-[:ANNOTATED_BY]->(namedQuery:Annotation),
              (namedQuery)-[:OF_TYPE]->(:Type{fqn:'jakarta.persistence.NamedQuery'}),
              (namedQuery)-[:HAS]->(nameAttribute:Value{name:'name'}),
              (namedQuery)-[:HAS]->(queryAttribute:Value{name:'query'})
            MERGE
              (entity)-[:DEFINES]->(n:Jpa:NamedQuery {name:nameAttribute.value})
            SET
              n.query=queryAttribute.value
            RETURN
              entity as Entity, n.name as Name, n.query as Query
        ]]></cypher>
    </concept>

</jqassistant-rules>
